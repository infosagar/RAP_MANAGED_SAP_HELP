managed;

define behavior for ZI_Travel_SP_M alias travel
implementation in class ZBP_TRAVEL_SP_M unique
persistent table /dmo/travel_m
with additional save
etag master last_changed_at
lock master

{
  mapping for /DMO/TRAVEL_M corresponding;

  create;
  update;
  delete;

  // administrative fields: read only
  field ( readonly ) last_changed_at, last_changed_by, created_at, created_by;

  // mandatory fields that are required to create a travel
  field ( mandatory ) agency_id, customer_id, begin_date, end_date, overall_status, booking_fee, currency_code;

  // dynamic field control
  field ( features : instance ) travel_id;

  // dynamic action control
  action ( features : instance ) acceptTravel result [1] $self;
  action ( features : instance ) rejectTravel result [1] $self;

  // dynamic operation control
  association _Booking { create ( features : instance ); }

  action createTravelByTemplate result [1] $self;

  validation validateCustomer on save { field customer_id; }
  validation validateAgency on save { field agency_id; }
  validation validateDates on save { field begin_date, end_date; }
  validation validateStatus on save { field overall_status; }

}

define behavior for ZI_Booking_SP_M alias booking
implementation in class ZBP_BOOKING_SP_M unique
persistent table /dmo/booking_m
etag master last_changed_at
lock dependent by _Travel


{
  //field ( readonly ) travel_id;

  mapping for /DMO/BOOKING_M corresponding;

  update;

  // static field control
  field ( mandatory ) carrier_id, connection_id, flight_date, booking_status;
  field ( readonly ) travel_id;

  // dynamic field control
  field ( features : instance ) booking_id, booking_date, customer_id;

  // dynamic operation control
  association _BookSupplement { create ( features : instance ); }
  association _Travel { }

  validation validateStatus on save { field booking_status; }

   // determination for calculation of total flight price
    determination calculateTotalFlightPrice on modify { field flight_price, currency_code; }
}


define behavior for ZI_BookSuppl_SP_M alias booksuppl
implementation in class ZBP_BOOKINGSUPPLEMENT_SP_M unique
//persistent table /dmo/booksuppl_m
with unmanaged save
etag master last_changed_at
lock dependent by _Travel

{
  // static field control
  field ( mandatory ) supplement_id, price;
  field ( readonly ) travel_id, booking_id;

  // dynamic field control
  field ( features : instance ) booking_supplement_id;

  mapping for /DMO/BOOKSUPPL_M corresponding;

  update;


  association _Travel { }

  determination calculateTotalSupplmPrice on modify { field price, currency_code; }
}